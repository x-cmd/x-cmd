# shellcheck disable=all

# Section: init
# zsh https://zsh.sourceforge.io/Doc/Release/Prompt-Expansion.html#Visual-effects

autoload -U colors && colors
autoload -Uz add-zsh-hook
zmodload zsh/datetime
______x_cmd_theme_setup_zsh_256color() {
    typeset -AHg FG BG
    # shellcheck disable=SC3009
    for _color in {000..255}; do
        # replace $'%{\033[38;5;31%}'
        FG[$_color]="%{[38;5;${_color}m%}"
        BG[$_color]="%{[48;5;${_color}m%}"
    done
    unset _color
}

______x_cmd_theme_setup_zsh_basecolor() {
    typeset -AHg fg_dim bg_dim fg_light bg_light
    for k in ${(k)color[(I)fg-*]}; do
        fg_dim[${k#fg-}]="%{[2;${color[$k]}m%}"
        fg_light[${k#fg-}]="%{[9${color[$k]#3}m%}"
    done
    for k in ${(k)color[(I)bg-*]}; do
        bg_dim[${k#bg-}]="%{[2;${color[$k]}m%}"
        bg_light[${k#bg-}]="%{[10${color[$k]#4}m%}"
    done
    unset k
}

______x_cmd_theme_setup_zsh_basecolor
if [ -z $FG[100] ];then
    ______x_cmd_theme_setup_zsh_256color
fi

# Hooks
_____x_cmd_theme_command_record(){
    # $EPOCHREALTIME from zsh module zsh/datetime
    [ -n "$EPOCHREALTIME" ] || return

    if [ -n "$___X_CMD_THEME_START_TIME" ]; then
        ___X_CMD_THEME_END_TIME=$EPOCHREALTIME
    else
        ___X_CMD_THEME_START_TIME=$EPOCHREALTIME
    fi
}

_____x_cmd_theme_post_command_record(){
    if [ -z "$___X_CMD_THEME_END_TIME" ];then
        unset ___X_CMD_THEME_START_TIME
        unset ___X_CMD_THEME_DURATION_SECOND
    elif [ -n "$___X_CMD_THEME_START_TIME" ]; then
        ___X_CMD_THEME_DURATION_SECOND=$((___X_CMD_THEME_END_TIME - ___X_CMD_THEME_START_TIME))
        unset ___X_CMD_THEME_START_TIME
    fi
}

_____x_cmd_theme_register_hook(){
    add-zsh-hook preexec _____x_cmd_theme_command_record
    add-zsh-hook precmd  _____x_cmd_theme_command_record
    add-zsh-hook precmd  _____x_cmd_theme_post_command_record
}

_____x_cmd_theme_unset_hook(){
    unset ___X_CMD_THEME_START_TIME
    unset ___X_CMD_THEME_END_TIME
    unset ___X_CMD_THEME_DURATION_SECOND

    add-zsh-hook -D preexec _____x_cmd_theme_command_record
    add-zsh-hook -D precmd _____x_cmd_theme_command_record
    add-zsh-hook -D precmd  _____x_cmd_theme_post_command_record
}
# EndSection

###
  # @description: set up style color
  # @example: ___x_cmd_theme_style fg-red bold hello world
###
___x_cmd_theme_style() {
    while [ $# -gt 0 ]; do
        case "$1" in
            bold)                                                   printf "%s" "%B"                                           ;;
            underline)                                              printf "%s" "%U"                                           ;;
            black|red|green|yellow|blue|magenta|cyan|white)         printf "%s" "%{$fg[$1]%}"                                  ;;
            fg-*)                                                   printf "%s" "%{$fg[${1#fg-}]%}"                            ;;
            bfg-*)                                                  printf "%s" "%{$fg_bold[${1#bfg-}]%}"                      ;;
            hfg-*)                                                  printf "%s" "%{$fg_light[${1#hfg-}]%}"                     ;;
            dfg-*)                                                  printf "%s" "%{$fg_dim[${1#dfg-}]%}"                       ;;
            colourfg-*)                                             printf "%s" "%{$FG[${1#colourfg-}]%}"                      ;;
            rgbfg-*)                                                local color_code=""; ___x_cmd_theme___ser_rgb_color_code "${1#rgbfg-}"; printf "%s" "%{%{[38;2;${color_code}m%}%}" ;;
            bg-*)                                                   printf "%s" "%{$bg[${1#bg-}]%}"                            ;;
            bbg-*)                                                  printf "%s" "%{$bg_bold[${1#bbg-}]%}"                      ;;
            hbg-*)                                                  printf "%s" "%{$bg_light[${1#hbg-}]%}"                     ;;
            dbg-*)                                                  printf "%s" "%{$bg_dim[${1#dbg-}]%}"                       ;;
            colourbg-*)                                             printf "%s" "%{$BG[${1#colourbg-}]%}"                      ;;
            rgbbg-*)                                                local color_code=""; ___x_cmd_theme___ser_rgb_color_code "${1#rgbbg-}"; printf "%s" "%{%{[48;2;${color_code}m%}%}" ;;
            reset)                                                  printf "%s" "%{$reset_color%}"                             ;;
            --)                                                     shift; printf "%s" "$*%{$reset_color%}%b%u"; return        ;;
            *)                                                      printf "%s" "$*%{$reset_color%}%b%u"; return
        esac
        shift
    done
}

_______x_cmd_theme_set_complate_color() {
    local color_code="${1:?Provide Color code e.g 32}"
    local x_cmd_theme_ls_color
    # add complate hightlight
    x_cmd_theme_ls_color="ma=7;$color_code:$LS_COLORS:"
    # add advise description hightlight
    x_cmd_theme_ls_color="=(#b) #([0-9]#*)(-- *)=0=$color_code=1;31:$x_cmd_theme_ls_color:"
    zstyle ':completion:*:*:x:*' list-colors "=^(*.*)=$color_code:"

    # set zstyle
    zstyle ':completion:*:default' list-colors "${(s.:.)x_cmd_theme_ls_color}"
}
